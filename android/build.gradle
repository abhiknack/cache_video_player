group 'io.flutter.plugins.videoplayer'
version '1.0-SNAPSHOT'

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace 'io.flutter.plugins.videoplayer'
    compileSdk = 34
    ndkVersion "26.3.11579264"

    defaultConfig {
        minSdkVersion 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        checkAllWarnings false
        warningsAsErrors false
        disable 'AndroidGradlePluginVersion', 'InvalidPackage', 'GradleDependency', 'NewerVersionAvailable'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:none'
        options.compilerArgs << '-nowarn'
    }

    dependencies {
        def exoplayer_version = "1.4.1"
        implementation "androidx.media3:media3-exoplayer:${exoplayer_version}"
        implementation "androidx.media3:media3-exoplayer-hls:${exoplayer_version}"
        implementation "androidx.media3:media3-exoplayer-dash:${exoplayer_version}"
        implementation "androidx.media3:media3-exoplayer-rtsp:${exoplayer_version}"
        implementation "androidx.media3:media3-exoplayer-smoothstreaming:${exoplayer_version}"
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'androidx.test:core:1.4.0'
        testImplementation 'org.mockito:mockito-inline:5.0.0'
        testImplementation 'org.robolectric:robolectric:4.10.3'
        testImplementation "androidx.media3:media3-test-utils:${exoplayer_version}"
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            // The org.gradle.jvmargs property that may be set in gradle.properties does not impact
            // the Java heap size when running the Android unit tests. The following property here
            // sets the heap size to a size large enough to run the robolectric tests across
            // multiple SDK levels.
            jvmArgs "-Xmx4G"
            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // Use a version compatible with your project
        classpath 'com.android.tools.build:gradle:7.3.0'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}
